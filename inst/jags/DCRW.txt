data {
	pi <- 3.141592653589
	pi2 <- 2*pi
	npi <- pi*-1

	Omega[1,1] <- 1
	Omega[1,2] <- 0
	Omega[2,1] <- 0
	Omega[2,2] <- 1
	
	first.loc[1] <- y[1,1]
	first.loc[2] <- y[1,2]
	}
model
{
#  "DCRW" model from Ian D Jonsen, Joanna Mills Flemming and Ransom A Myers
#    Robust state-space modeling of animal movement data
#    ijonsen@dal.ca
#    Created by Ian Jonsen, 10/29/2004, last modified, 01/08/2013


## priors on process uncertainty
iSigma[1:2,1:2] ~ dwish(Omega[,],2)	
Sigma[1:2,1:2] <- inverse(iSigma[,])

## Priors for first location
for(k in 1:2){
	x[1,k] ~ dt(first.loc[k], itau2[1,k] * psi, nu[1,k])
	}

## Assume simple random walk to estimate 2nd regular position
x[2,1:2] ~ dmnorm(x[1,], iSigma[,])

theta ~ dunif(npi,pi)	
gamma ~ dbeta(1,1)	
logpsi ~ dunif(-10, 10)		
psi <- exp(logpsi)


## Transition equation
for(t in 2:(RegN-1)){
	## Build transition matrix for rotational component	
	T[t,1,1] <- cos(theta)
	T[t,1,2] <- -sin(theta)
	T[t,2,1] <- sin(theta)
	T[t,2,2] <- cos(theta)
	for(k in 1:2){
		Tdx[t,k] <- T[t,k,1] * (x[t,1] - x[t-1,1]) + T[t,k,2] * (x[t,2] - x[t-1,2])	
		x.mn[t,k] <- x[t,k] + gamma * Tdx[t,k]	
		}
			
	x[t+1,1:2] ~ dmnorm(x.mn[t,], iSigma[,])	
	}

##	Measurement equation
for(t in 2:RegN){				
	for(i in idx[t-1]:(idx[t]-1)){		
		for(k in 1:2){
			
			zhat[i,k] <- (1-j[i]) * x[t-1,k] + j[i] * x[t,k]
			y[i,k] ~ dt(zhat[i,k], itau2[i,k] * psi, nu[i,k])	
			}
		}
	}	
}


